
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьКомпоненту(Неопределено);
	
	Если НЕ ЗначениеЗаполнено(Объект.УровеньЛогирования) Тогда
		Объект.УровеньЛогирования = "info";	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	СообщитьПользователю("Внешнее событие " + Источник + ", " + Событие + ", " + Данные);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьКомпоненту(Команда)
	
	Если НЕ ПодключитьКомпонентуНаСервере() Тогда
		ПоказатьПредупреждение(, "Не удалось подключить компоненту", , "Внимание!");	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПодключитьКомпонентуНаСервере()
	
	Результат = ПодключитьВнешнююКомпоненту("c:\git\RdKafka1C\build\Debug\RdKafka1C.dll", "Kafka", 
		ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.Изолированно);
		
	Возврат Результат;	
		
КонецФункции

&НаКлиенте
Процедура Остановить(Команда)
	ОстановитьНаСервере();
	ПрочитатьЛогиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОстановитьНаСервере()
	
	Компонента = ВосстановитьКомпоненту();
	
	Результат = Компонента.ОстановитьПродюсера();
	Если НЕ Результат Тогда
		СообщитьПользователю(Компонента.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	Результат = Компонента.ОстановитьКонсюмера();
	Если НЕ Результат Тогда
		СообщитьПользователю(Компонента.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	Компонента = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура Запустить(Команда)	
	
	ЗапуститьНаСервере();
	ПрочитатьЛогиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьНаСервере()	
	
	Компонента = Новый ("AddIn.Kafka.RdKafka1C");	
	Компонента.ТаймаутОпераций = 1000;
	Объект.ВерсияКомпоненты = Компонента.ВерсияКомпоненты;
	Объект.ВерсияRdKafka = Компонента.ВерсияRdKafka;
		
	Если ЗначениеЗаполнено(Объект.КаталогЛогов) 
		И ЗначениеЗаполнено(Объект.УровеньЛогирования)
		И Объект.УровеньЛогирования <> "none"
		Тогда
		Если Компонента.ВключитьЛогирование(Объект.КаталогЛогов, Объект.УровеньЛогирования) Тогда
			Объект.ЛогФайл = Компонента.ЛогФайл;	
		Иначе
			СообщитьПользователю(Компонента.ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
			
	Результат = Компонента.ИнициализироватьПродюсера(Объект.Брокер, Объект.Топик);
	Если НЕ Результат Тогда
		СообщитьПользователю(Компонента.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	Результат = Компонента.ИнициализироватьКонсюмера(Объект.Брокер, Объект.Топик, Объект.ГруппаКонсюмера);
	Если НЕ Результат Тогда
		СообщитьПользователю(Компонента.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	СохранитьКомпоненту(Компонента); 
		
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЛоги(Команда)
	ПрочитатьЛогиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЛогиНаСервере()
	
	Если НЕ ЗначениеЗаполнено(Объект.ЛогФайл) Тогда
		Логи.Очистить();
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(Объект.ЛогФайл);
	Если НЕ Файл.Существует() Тогда
		Логи.Очистить();
		Возврат;
	КонецЕсли;
	
	Логи.Прочитать(Объект.ЛогФайл);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	
	ОтправитьСообщениеНаСервере();
	ПрочитатьЛогиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьСообщениеНаСервере()
	
	Компонента = ВосстановитьКомпоненту();
	
	Результат = Компонента.Отправить(ТекстСообщения, "", 0);
	Если НЕ Результат Тогда
		СообщитьПользователю(Компонента.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	СохранитьКомпоненту(Компонента);	
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьСообщения(Команда)
	ПрочитатьСообщениеНаСервере();
	ПрочитатьЛогиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСообщениеНаСервере()
	
	Компонента = ВосстановитьКомпоненту();
	
	Пока Компонента.Прочитать() Цикл		
		Текст = СтрШаблон(
			"Сообщение: '%1'
			|Метаданные: %2",
			Компонента.ДанныеСообщения(),
			Компонента.МетаданныеСообщения());
		СообщитьПользователю(Текст);		
	КонецЦикла;
	    
	Если ЗначениеЗаполнено(Компонента.ОписаниеОшибки) Тогда
		СообщитьПользователю(Компонента.ОписаниеОшибки);
	КонецЕсли;
	
	СохранитьКомпоненту(Компонента);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьПользователю(Текст)

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьКомпоненту(Компонента)

	Контейнер = Новый Структура("Компонента", Компонента);
	АдресКомпоненты = ПоместитьВоВременноеХранилище(Контейнер, УникальныйИдентификатор);	
	
КонецПроцедуры

&НаСервере
Функция ВосстановитьКомпоненту()

	Контейнер = ПолучитьИзВременногоХранилища(АдресКомпоненты);
	Возврат Контейнер.Компонента;
	
КонецФункции

&НаКлиенте
Процедура УровеньЛоговПриИзменении(Элемент)
	УровеньЛоговПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура УровеньЛоговПриИзмененииНаСервере()
	
	Компонента = ВосстановитьКомпоненту();
	Компонента.УровеньЛогирования = Объект.УровеньЛогирования;
	
	СохранитьКомпоненту(Компонента);
	
КонецПроцедуры

#КонецОбласти









