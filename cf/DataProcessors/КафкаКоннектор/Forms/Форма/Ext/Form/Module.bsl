
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УровеньЛогирования = "none";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьЭлементы();	
	ПодключитьКомпонентуНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВключитьЛогированиеПриИзменении(Элемент)
	ВключитьЛогированиеПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВключитьЛогированиеПриИзмененииНаСервере()
	
	Если ВключитьЛогирование Тогда
		Коннектор = ВосстановитьКоннектор();
		ЛогФайл = Коннектор.НачатьЛогирование(УровеньЛогирования);
		СохранитьКоннектор(Коннектор);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПартицияСообщенияАвтоПриИзменении(Элемент)
	ОбновитьЭлементы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьКомпоненту(Команда)	
	ПодключитьКомпонентуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПодключитьКомпонентуНаСервере()

	Если НЕ ЗначениеЗаполнено(НастройкиКафки) Тогда
		Возврат;	
	КонецЕсли;
	
	Коннектор = Обработки.КафкаКоннектор.Создать();
	
	Если ВключитьЛогирование Тогда
		ЛогФайл = Коннектор.НачатьЛогирование(УровеньЛогирования);
	КонецЕсли;
	
	Коннектор.Инициализировать(НастройкиКафки.Идентификатор);
	
	СохранитьКоннектор(Коннектор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьКомпоненту(Команда)
	ОтключитьКомпонентуНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтключитьКомпонентуНаСервере()
	
	Коннектор = ВосстановитьКоннектор();
	Коннектор.Остановить();
	Коннектор = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЛоги(Команда)
	ПрочитатьЛогиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЛогиНаСервере()
	
	Логи.Очистить();
	Если НЕ ЗначениеЗаполнено(ЛогФайл) Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ЛогФайл);
	Если НЕ Файл.Существует() Тогда
		Возврат;
	КонецЕсли;
	
	Логи.Прочитать(ЛогФайл);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщения(Команда)
	ОтправитьСообщенияНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ОтправитьСообщенияНаСервере()
	
	Коннектор = ВосстановитьКоннектор();
	
	ДобавлятьНомерСообщения = КоличествоСообщенийКОтправке > 1 И Лев(СокрЛП(ТекстСообщенияКОтправке), 1) <> "{";
	
	ВремяНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	Для НомерСообщения = 1 По КоличествоСообщенийКОтправке Цикл
		Текст = ТекстСообщенияКОтправке;
		Если ДобавлятьНомерСообщения Тогда
			Текст = Текст + " " + НомерСообщения; 	
		КонецЕсли;
		
		Если ПартицияСообщенияАвто Тогда
			Коннектор.ОтправитьСообщение(Текст, КлючСообщения, ЗаголовкиСообщения);
		Иначе
			Коннектор.ОтправитьСообщение(Текст, КлючСообщения, ЗаголовкиСообщения, ПартицияСообщения);
		КонецЕсли;
					
	КонецЦикла;
	
	ВремяОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	ТекстСообщения = СтрШаблон("Отправлено %1 сообщений за %2 секунд", 
	 	КоличествоСообщенийКОтправке,
		(ВремяОкончания - ВремяНачала) / 1000);
	СообщитьПользователю(ТекстСообщения);
	
	СохранитьКоннектор(Коннектор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщенияАсинхронно(Команда)
	ОтправитьСообщенияАсинхронноНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтправитьСообщенияАсинхронноНаСервере()
	
	Коннектор = ВосстановитьКоннектор();
	
	ДобавлятьНомерСообщения = КоличествоСообщенийКОтправке > 1 И Лев(СокрЛП(ТекстСообщенияКОтправке), 1) <> "{";
	
	МассивСообщений = Новый Массив;
	Для НомерСообщения = 1 По КоличествоСообщенийКОтправке Цикл
		Текст = ТекстСообщенияКОтправке;
		Если ДобавлятьНомерСообщения Тогда
			Текст = Текст + " " + НомерСообщения; 	
		КонецЕсли;
		
		Если ПартицияСообщенияАвто Тогда
			Сообщение = Коннектор.НовоеСообщение(Текст, КлючСообщения, ЗаголовкиСообщения);
		Иначе
			Сообщение = Коннектор.НовоеСообщение(Текст, КлючСообщения, ЗаголовкиСообщения, ПартицияСообщения);
		КонецЕсли;
		
		МассивСообщений.Добавить(Сообщение);					
	КонецЦикла;
	
	ВремяНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Коннектор.ОтправитьСообщения(МассивСообщений);
	ВремяОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	ТекстСообщения = СтрШаблон("Отправлено %1 сообщений за %2 секунд", 
	 	КоличествоСообщенийКОтправке,
		(ВремяОкончания - ВремяНачала) / 1000);
	СообщитьПользователю(ТекстСообщения);
	
	СохранитьКоннектор(Коннектор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьСообщения(Команда)
	ПрочитатьСообщениеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСообщениеНаСервере()
	
	Коннектор = ВосстановитьКоннектор();	
	
	// Сначала вычитываем все сообщения для замера времени
	ВремяНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	МассивСообщений = Новый Массив;
	Сообщение = Коннектор.ПрочитатьСообщение();	
	Пока ЗначениеЗаполнено(Сообщение) Цикл		
		Если ЧитатьМетаданныеСообщений Тогда
			Сообщение = Сообщение + Символы.ПС + "Метаданные: " + Символы.ПС + Коннектор.ПрочитатьМетаданныеСообщения();	
		КонецЕсли;
		
		МассивСообщений.Добавить(Сообщение);
		Если МассивСообщений.Количество() >= КоличествоСообщенийКЧтению Тогда
			Прервать;	
		КонецЕсли;
		
		Сообщение = Коннектор.ПрочитатьСообщение();
	КонецЦикла;
	
	ВремяОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	ТекстСообщения = СтрШаблон("Загружено %1 сообщений за %2 секунд", 
	 	МассивСообщений.Количество(),
		(ВремяОкончания - ВремяНачала) / 1000);
	СообщитьПользователю(ТекстСообщения);
	
	// Теперь выводим сообщения, если количество больше 100 - это явно нагрузочный тест, нет смысла выводить их
	Если МассивСообщений.Количество() <= 100 Тогда
		Для каждого Сообщение Из МассивСообщений Цикл
			СообщитьПользователю(СтрШаблон("Сообщение: '%1'", Сообщение));
		КонецЦикла;	
	КонецЕсли;
		    
	СохранитьКоннектор(Коннектор);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗафиксироватьОффсет(Команда)
	ЗафиксироватьОффсетНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗафиксироватьОффсетНаСервере()
	
	Коннектор = ВосстановитьКоннектор();
	Коннектор.ЗафиксироватьОффсет(ПартицияСообщения, Оффсет);
	СохранитьКоннектор(Коннектор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчередьСообщенийПродюсера(Команда)
	ОчередьСообщенийПродюсераНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОчередьСообщенийПродюсераНаСервере()
	
	Коннектор = ВосстановитьКоннектор();
	СообщитьПользователю("Очередь сообщений продюсера: " + Коннектор.ОчередьСообщенийПродюсера());
	СохранитьКоннектор(Коннектор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчередьСообщенийКонсюмера(Команда)
	ОчередьСообщенийКонсюмераНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОчередьСообщенийКонсюмераНаСервере()
	
	Коннектор = ВосстановитьКоннектор();
	СообщитьПользователю("Очередь сообщений консюмера: " + Коннектор.ОчередьСообщенийКонсюмера());
	СохранитьКоннектор(Коннектор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьЭлементы()
	
	Элементы.ПартицияОтправка.Доступность = НЕ ПартицияСообщенияАвто;	
	Элементы.ПартицияПолучение.Доступность = НЕ ПартицияСообщенияАвто;	
	
КонецПроцедуры

&НаСервере
Процедура СохранитьКоннектор(Коннектор)
	
	Структура = Новый Структура("Коннектор", Коннектор);
	АдресКоннектора = ПоместитьВоВременноеХранилище(Структура, УникальныйИдентификатор);	
	
КонецПроцедуры

&НаСервере
Функция ВосстановитьКоннектор()
	
	Структура = ПолучитьИзВременногоХранилища(АдресКоннектора);
	Возврат Структура.Коннектор;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СообщитьПользователю(Текст)

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти
