
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Настройки", Настройки);
		
	Заголовок = СтрШаблон("Логи настройки Кафки: %1", Настройки);
	Каталог = Справочники.НастройкиКафки.КаталогЛогов(Настройки);
	
	ОбновитьФайлыНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ФайлыПриАктивизацииСтроки(Элемент)
	
	ПрочитатьЛоги();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ФайлыПередУдалением(Элемент, Отказ)

	УдалитьФайлыНаСервере(Элементы.Файлы.ВыделенныеСтроки);
	ПрочитатьЛоги();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФайлы(Команда)
	ОбновитьФайлыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьФайлыНаСервере()
	
	Файлы.Очистить();
	
	НайденныеФайлы = НайтиФайлы(Каталог, "*.log");
	Для каждого Файл Из НайденныеФайлы Цикл
		НоваяСтрока = Файлы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Файл);
		
		Размер = Файл.Размер();
		НоваяСтрока.Размер = Размер;
		
		Если Размер <= 1000 Тогда
			НоваяСтрока.РазмерПредставление = Формат(Размер, "ЧДЦ=0; ЧС=0; ЧФ='Ч'");
			НоваяСтрока.Предупреждать = Ложь;
		ИначеЕсли Размер <= 1000000 Тогда
			НоваяСтрока.РазмерПредставление = Формат(Размер, "ЧДЦ=0; ЧС=3; ЧФ='Ч Кб'");
			НоваяСтрока.Предупреждать = Ложь;
		ИначеЕсли Размер <= 1000000000 Тогда
			НоваяСтрока.РазмерПредставление = Формат(Размер, "ЧДЦ=0; ЧС=6; ЧФ='Ч Мб'");
			НоваяСтрока.Предупреждать = Истина;
		Иначе
			НоваяСтрока.РазмерПредставление = Формат(Размер, "ЧДЦ=0; ЧС=9; ЧФ='Ч Гб'");
			НоваяСтрока.Предупреждать = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Файлы.Сортировать("Имя Убыв");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПрочитатьЛоги()
	
	Логи.Очистить();
	Если Элементы.Файлы.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбраннаяСтрока = Элементы.Файлы.ТекущиеДанные;
	ЛогФайл = Каталог + ВыбраннаяСтрока.Имя;
	Если НЕ ЗначениеЗаполнено(ЛогФайл) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбраннаяСтрока.Предупреждать Тогда
		ДополнительныеПараметры = Новый Структура("ЛогФайл", ЛогФайл);
		Оповещение = Новый ОписаниеОповещения("ПрочитатьЛогиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ТекстСообщения = СтрШаблон("Чтение файла %1 размером %2 может занять время. Прочитать файл?", 
			ВыбраннаяСтрока.Имя, ВыбраннаяСтрока.РазмерПредставление);
		ПоказатьВопрос(Оповещение, ТекстСообщения, РежимДиалогаВопрос.ДаНет, , , "Внимание!");
	Иначе
		ПрочитатьЛогиНаСервере(ЛогФайл);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЛогиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьЛогиНаСервере(ДополнительныеПараметры.ЛогФайл);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЛогиНаСервере(ЛогФайл)
	
	Файл = Новый Файл(ЛогФайл);
	
	Если НЕ Файл.Существует() Тогда
		Возврат;
	КонецЕсли;
	
	Логи.Прочитать(ЛогФайл);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьФайлыНаСервере(Знач ИдентификаторыСтрок)
	
	Для каждого Идентификатор Из ИдентификаторыСтрок Цикл
		Строка = Файлы.НайтиПоИдентификатору(Идентификатор);
		УдалитьФайлы(Строка.ПолноеИмя);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
